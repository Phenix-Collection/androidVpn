apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "nl.eduvpn.app"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 2
        versionName "1.0.1"
        buildConfigField "String", "INSTANCE_LIST_URL", "\"https://static.eduvpn.nl/instances.json\""
        buildConfigField "String", "FEDERATION_LIST_URL", "\"https://static.eduvpn.nl/federation.json\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        // Android Studio uses the alphabetically first flavor as the default, so I selected a name which would precede "dev" (sadly, "default" is a reserved keyword)
        basic {
        }
        dev {
            // Override the URLs for fetching the configs
            buildConfigField "String", "INSTANCE_LIST_URL", "\"https://static.eduvpn.nl/instances-dev.json\""
            buildConfigField "String", "FEDERATION_LIST_URL", "\"https://static.eduvpn.nl/federation-dev.json\""
        }
    }

    lintOptions {
        disable 'GradleDependency', // Gradle dependencies can be a bit outdated, since we prefer to use the same versions as in the VPN library
                'UnsafeNativeCodeLocation', // The OpenVPN .so files are put to a different place, as per the documentation
                'RtlSymmetry', 'RtlHardcoded' // No support for RTL as of now
    }
}

def supportLibVersion = "25.4.0" // Please try to stay in sync with the version used in the ics-openvpn module

dependencies {
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:customtabs:$supportLibVersion"
    // HTTP client
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    // View injection
    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
    // Dependency injection
    compile 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    // Image loader
    compile 'com.squareup.picasso:picasso:2.5.2'

    // The VPN library
    compile project(':ics-openvpn-main')

    // All testing related libraries
    testCompile 'junit:junit:4.12'
    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}
