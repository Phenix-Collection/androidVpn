apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "nl.eduvpn.app"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 2
        versionName "1.0.1"
        buildConfigField "String", "SECURE_INTERNET_DISCOVERY_URL", "\"https://static.eduvpn.nl/disco/secure_internet.json\""
        buildConfigField "String", "INSTITUTE_ACCESS_DISCOVERY_URL", "\"https://static.eduvpn.nl/disco/institute_access.json\""
        buildConfigField "String", "SIGNATURE_URL_POSTFIX", "\".sig\""
        buildConfigField "String", "SIGNATURE_VALIDATION_PUBLIC_KEY", "\"E5On0JTtyUVZmcWd+I/FXRm32nSq8R2ioyW7dcu/U88=\""
        buildConfigField "boolean", "API_DISCOVERY_ENABLED", "true"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'org.eduvpn.app'
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        // Android Studio uses the alphabetically first flavor as the default, so I selected a name which would precede "dev" (sadly, "default" is a reserved keyword)
        basic {
        }
        dev {
            applicationId "nl.eduvpn.app.dev"
            // Override the URLs for fetching the configs
            buildConfigField "String", "SECURE_INTERNET_DISCOVERY_URL", "\"https://static.eduvpn.nl/disco/secure_internet_dev.json\""
            buildConfigField "String", "INSTITUTE_ACCESS_DISCOVERY_URL", "\"https://static.eduvpn.nl/disco/institute_access_dev.json\""
        }
        home {
            applicationId "nl.eduvpn.app.home"
            // API discovery is disabled, and only custom URLs can be entered.
            buildConfigField "boolean", "API_DISCOVERY_ENABLED", "false"
        }
    }

    lintOptions {
        disable 'GradleDependency', // Gradle dependencies can be a bit outdated, since we prefer to use the same versions as in the VPN library
                'UnsafeNativeCodeLocation', // The OpenVPN .so files are put to a different place, as per the documentation
                'RtlSymmetry', 'RtlHardcoded', // No support for RTL as of now
                'MissingTranslation' // The OpenVPN library contains translations for a lot off languages. This app only has english, so it complains that we are missing translations for the other languages
    }
}

def supportLibVersion = "25.4.0" // Please try to stay in sync with the version used in the ics-openvpn module

dependencies {
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:customtabs:$supportLibVersion"
    // OAuth client
    compile 'net.openid:appauth:0.6.1'
    // Secure preferences
    compile 'com.scottyab:secure-preferences-lib:0.1.4'
    // HTTP client
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    // View injection
    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
    // Dependency injection
    compile 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    // Image loader
    compile 'com.squareup.picasso:picasso:2.5.2'

    // The VPN library
    compile project(':ics-openvpn-main')
    // Public key verification
    compile "com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:1.0.6@aar"
    // Reactive extensions for asynchronous events
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.2'

    // All testing related libraries
    testCompile 'junit:junit:4.12'
    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}
