apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "net.tuxed.vpnconfigimporter"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "INSTANCE_LIST_URL", "\"https://www.eduvpn.nl/instances.json\""
    }
    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets', 'src/main/ovpnlibs/assets']
            jniLibs.srcDirs = ['src/main/ovpnlibs/lib']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'GradleDependency', // Gradle dependencies can be a bit outdated, since we prefer to use the same versions as in the VPN library
                'UnsafeNativeCodeLocation', // The OpenVPN so files are put to a different place, as per the documentation
                'RtlSymmetry', 'RtlHardcoded' // No support for RTL as of now
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    // UI
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.android.support:customtabs:24.2.0'
    // View injection
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    // Dependency injection
    compile 'com.google.dagger:dagger:2.7'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.7'
    // Custom fonts
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    // Image loader
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile project(':ics-openvpn-main')
}
