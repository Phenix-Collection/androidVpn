apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "nl.eduvpn.app"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 4
        versionName "1.1.1"
        buildConfigField "String", "SECURE_INTERNET_DISCOVERY_URL", "\"https://static.eduvpn.nl/disco/secure_internet.json\""
        buildConfigField "String", "INSTITUTE_ACCESS_DISCOVERY_URL", "\"https://static.eduvpn.nl/disco/institute_access.json\""
        buildConfigField "String", "SIGNATURE_URL_POSTFIX", "\".sig\""
        buildConfigField "String", "SIGNATURE_VALIDATION_PUBLIC_KEY", "\"E5On0JTtyUVZmcWd+I/FXRm32nSq8R2ioyW7dcu/U88=\""
        buildConfigField "boolean", "API_DISCOVERY_ENABLED", "true"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'org.eduvpn.app'
        ]
        missingDimensionStrategy 'implementation', 'noovpn3'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "product"

    productFlavors {
        // Android Studio uses the alphabetically first flavor as the default, so I selected a name which would precede "dev" (sadly, "default" is a reserved keyword)
        basic {
            dimension "product"
        }
        dev {
            dimension "product"
            applicationId "nl.eduvpn.app.dev"
            // Override the URLs for fetching the configs
            buildConfigField "String", "SECURE_INTERNET_DISCOVERY_URL", "\"https://static.eduvpn.nl/disco/secure_internet_dev.json\""
            buildConfigField "String", "INSTITUTE_ACCESS_DISCOVERY_URL", "\"https://static.eduvpn.nl/disco/institute_access_dev.json\""
            buildConfigField "String", "SIGNATURE_VALIDATION_PUBLIC_KEY", "\"zzls4TZTXHEyV3yxaxag1DZw3tSpIdBoaaOjUGH/Rwg=\""
        }
        home {
            dimension "product"
            applicationId "nl.eduvpn.app.home"
            // API discovery is disabled, and only custom URLs can be entered.
            buildConfigField "boolean", "API_DISCOVERY_ENABLED", "false"
        }
    }

    lintOptions {
        disable 'GradleDependency', // Gradle dependencies can be a bit outdated, since we prefer to use the same versions as in the VPN library
                'UnsafeNativeCodeLocation', // The OpenVPN .so files are put to a different place, as per the documentation
                'RtlSymmetry', 'RtlHardcoded', // No support for RTL as of now
                'MissingTranslation' // The OpenVPN library contains translations for a lot off languages. This app only has english, so it complains that we are missing translations for the other languages
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

def supportLibVersion = "27.1.1" // Please try to stay in sync with the version used in the ics-openvpn module
def butterknifeVersion = "8.8.1"
def daggerVersion = "2.15"

dependencies {
    // The VPN library
    implementation project(path: ':ics-openvpn-main')

    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    // OAuth client
    implementation 'net.openid:appauth:0.6.1'
    // Secure preferences
    implementation 'com.scottyab:secure-preferences-lib:0.1.4'
    // HTTP client
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    // View injection
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    // Dependency injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    // Image loader
    implementation 'com.squareup.picasso:picasso:2.5.2'
    // Public key verification
    implementation "com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:1.0.6@aar"
    // Reactive extensions for asynchronous events
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.2'

    // All testing related libraries
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support:support-annotations:$supportLibVersion"
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
}
